include(FindPackageHandleStandardArgs)
set(${CMAKE_FIND_PACKAGE_NAME}_CONFIG ${CMAKE_CURRENT_LIST_FILE})
find_package_handle_standard_args(@PROJECT_NAME@ CONFIG_MODE)

if(NOT TARGET @PROJECT_NAME@)
    macro(find_qt_dependency)
        set(options REQUIRED)
        set(oneValueArgs "")
        set(multiValueArgs COMPONENTS)

        cmake_parse_arguments(MY_OPTIONS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

        if(NOT MY_OPTIONS_COMPONENTS) # User didn't enter any component
            set(MY_OPTIONS_COMPONENTS "")
        endif()

        if(NOT QT_DEFAULT_MAJOR_VERSION OR QT_DEFAULT_MAJOR_VERSION STREQUAL "6")
        find_package(Qt6 COMPONENTS ${MY_OPTIONS_COMPONENTS} QUIET)
        endif()
        if(NOT Qt6_FOUND)
            if(${MY_OPTIONS_REQUIRED})
                find_package(Qt5 5.15 COMPONENTS ${MY_OPTIONS_COMPONENTS} REQUIRED)
            else()
                find_package(Qt5 5.15 COMPONENTS ${MY_OPTIONS_COMPONENTS})
            endif()
        endif()
    endmacro()

    find_qt_dependency(COMPONENTS Core Widgets REQUIRED)

    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
endif()
